pragma solidity ^0.5.0;
pragma experimental ABIEncoderV2;

import "./ReportHandle.sol";
import "./utils/Secp256k1.sol";
import "./utils/BytesUtils.sol";

// Consider: Avoid inheritting
contract Anonify is ReportHandle {
    using BytesUtils for bytes;

    struct GroupKeyCounter {
        uint32 generation;
        uint32 epoch;
    }

    address private _owner;
    // A version of enclave binary
    uint32 private _mrenclaveVer;
    // An counter of registered roster index
    uint32 private _rosterIdxCounter;
    // Counter for enforcing the order of state transitions
    uint256 private _stateCounter;
    // Counter for enforcing the order of state transitions
    mapping(uint32 => GroupKeyCounter) private _groupKeyCounter;
    // Mapping of a sender and roster index
    mapping(address => uint32) private _senderToRosterIdx;

    event StoreCiphertext(bytes ciphertext, uint256 stateCounter);
    event StoreHandshake(bytes handshake, uint256 stateCounter);
    event UpdateMrenclaveVer(uint32 newVersion);

    constructor(
        bytes memory _report,
        bytes memory _reportSig,
        bytes memory _handshake,
        uint32 mrenclaveVer,
        uint32 _rosterIdx
    ) public ReportHandle(_report, _reportSig) {
        require(_rosterIdx == 0, "First roster_idx must be zero");

        _owner = msg.sender;
        _mrenclaveVer = mrenclaveVer;
        _senderToRosterIdx[msg.sender] = _rosterIdx;
        _rosterIdxCounter = _rosterIdx;
        storeHandshake(_handshake);
    }

    modifier onlyOwner() {
        require(_owner == msg.sender, "caller is not the owner");
        _;
    }

    // a new TEE node joins the group.
    function joinGroup(
        bytes memory _report,
        bytes memory _reportSig,
        bytes memory _handshake,
        uint32 _version,
        uint32 _rosterIdx
    ) public {
        require(_mrenclaveVer == _version, "Must be same version");
        require(
            _rosterIdx == _rosterIdxCounter + 1,
            "Joining the group must be ordered accordingly by roster index"
        );
        require(
            _senderToRosterIdx[msg.sender] == 0,
            "The msg.sender can join only once"
        );

        handleReport(_report, _reportSig);
        _senderToRosterIdx[msg.sender] = _rosterIdx;
        _rosterIdxCounter = _rosterIdx;
        storeHandshake(_handshake);
    }

    // a recovered TEE node registers the report
    function registerReport(
        bytes memory _report,
        bytes memory _reportSig,
        uint32 _version,
        uint32 _rosterIdx
    ) public {
        require(_mrenclaveVer == _version, "Must be same version");

        handleReport(_report, _reportSig);
        _senderToRosterIdx[msg.sender] = _rosterIdx;
    }

    function updateMrenclave(
        bytes memory _report,
        bytes memory _reportSig,
        bytes memory _handshake,
        uint32 _newVersion,
        uint32 _rosterIdx
    ) public onlyOwner {
        require(_mrenclaveVer != _newVersion, "Must be new version");
        require(_rosterIdx == 0, "Only owner can update mrenclave");

        updateMrenclaveInner(_report, _reportSig);
        _mrenclaveVer = _newVersion;
        storeHandshake(_handshake);
        emit UpdateMrenclaveVer(_newVersion);
    }

    // Store ciphertexts which is generated by trusted environment.
    function storeCommand(
        bytes memory _newCiphertext,
        bytes memory _enclaveSig,
        uint32 _rosterIdx,
        uint32 _generation,
        uint32 _epoch
    ) public {
        address verifyingKey =
            Secp256k1.recover(
                sha256(abi.encodePacked(_newCiphertext, _rosterIdx, _generation, _epoch)),
                _enclaveSig
            );
        require(
            verifyingKey != address(0),
            "recovered verifyingKey was address(0)"
        );
        require(
            verifyingKeyMapping[verifyingKey] == verifyingKey,
            "Invalid enclave signature."
        );
        require(
            _generation > _groupKeyCounter[_rosterIdx].generation,
            "generation must be bigger than the counter"
        );
        require(
            _epoch == _groupKeyCounter[_rosterIdx].epoch,
            "epoch must be equal with the counter"
        );

        uint256 incremented_state_counter = _stateCounter.add(1);

        _groupKeyCounter[_rosterIdx] = GroupKeyCounter(_generation, _epoch);
        _stateCounter = incremented_state_counter;
        emit StoreCiphertext(_newCiphertext, incremented_state_counter);
    }

    function handshake(
        bytes memory _handshake,
        bytes memory _enclaveSig,
        uint32 _rosterIdx,
        uint32 _generation,
        uint32 _epoch
    ) public {
        require(
            _senderToRosterIdx[msg.sender] == _rosterIdx,
            "The roster index must be same as the registered one"
        );
        address verifyingKey =
            Secp256k1.recover(
                sha256(abi.encodePacked(_handshake, _rosterIdx, _generation, _epoch)),
                _enclaveSig
            );
        require(
            verifyingKey != address(0),
            "recovered verifyingKey was address(0)"
        );
        require(
            verifyingKeyMapping[verifyingKey] == verifyingKey,
            "Invalid enclave signature."
        );
        require(
            _generation == 0,
            "generation must be zero"
        );
        require(
            _epoch > _groupKeyCounter[_rosterIdx].epoch,
            "epoch must be bigger than the counter"
        );

        _groupKeyCounter[_rosterIdx] = GroupKeyCounter(_generation, _epoch);
        storeHandshake(_handshake);
    }

    function storeHandshake(bytes memory _handshake) private {
        uint256 incremented_state_counter = _stateCounter.add(1);
        _stateCounter = incremented_state_counter;
        emit StoreHandshake(_handshake, incremented_state_counter);
    }
}
